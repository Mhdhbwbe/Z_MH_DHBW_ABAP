managed implementation in class zbp_r_event_g3 unique;
strict(2);

define behavior for ZR_Event_G3 alias Event
persistent table zeventa
lock master
authorization master( instance )
etag master LastChangedAt
{
  create;
  update;
  delete;

  /* READ ONLY FIELDS */
  field (readonly, numbering: managed) EventUuid;
  field (readonly) EventId;
  field (readonly) Status;

  /* REQUIRED FIELDS */
  field (mandatory) Title, Location, StartDate, EndDate, MaxParticipants, Description;

  /* DETERMINATIONS */
  determination assign_event_id    on modify { create; }
  determination set_default_status on modify { create; }

  /* VALIDATIONS */
  validation check_start_date on save { create; update; }
  validation check_end_date   on save { create; update; }

  /* ACTIONS */
  action open_event  result [1] $self;
  action close_event result [1] $self;
}